curr_version := "flutter_breez_liquid-v" + `awk '/^version: /{print $2}' pubspec.yaml`
frb_bin := "flutter_rust_bridge_codegen generate"

export CARGO_TERM_COLOR := "always"

# Lists available recipes
default:
  just --list --unsorted

# Initializes the workspace
bootstrap frb='true' build='false':
	@if {{frb}} == true; then just frb; fi
	just init
	@if {{build}} == true; then just build; fi

# Install flutter_rust_bridge_codegen dependencies
frb:
	# This is locked plist PR https://github.com/ebarnard/rust-plist/pull/152 is merged/released
	cargo install cargo-expand --locked
	cargo install flutter_rust_bridge_codegen --version 2.9.0 --locked
	dart pub global activate ffigen
	dart pub global activate ffi
	cargo install cargo-xcode

init:
	just init-sdk

# Install Breez Liquid SDK dependencies
init-sdk:
	brew install protobuf

# Builds the uniFFI library & Generates Dart/Flutter bindings
build:
	just build-uniffi
	just gen

# Builds the uniFFI library
build-uniffi:
	just build-uniffi-android
	just build-uniffi-swift

# Builds the uniFFI library for Android
build-uniffi-android:
	bash scripts/build_uniffi_android.sh

# Builds the uniFFI library for Swift
build-uniffi-swift:
	bash scripts/build_uniffi_swift.sh

# Generate Dart/Flutter bindings & Softlinks C headers
gen ios='true':
	just codegen
	@if {{ios}} == true; then just build-ios-framework; fi

# Generate Dart/Flutter bindings & softlinks C headers
codegen:
	mkdir -p ../lib/src/rust
	{{frb_bin}}

# Builds the uniFFI framework & softlinks C headers
build-ios-framework:
	bash scripts/build_ios_framework.sh
	-ln -sf ../../lib/bindings/langs/flutter/breez_sdk_liquidFFI/include/breez_sdk_liquidFFI.h ../../packages/flutter_breez_liquid/ios/Classes/breez_sdk_liquidFFI.h
	-ln -sf ../../lib/bindings/langs/flutter/breez_sdk_liquidFFI/include/breez_sdk_liquidFFI.h ../../packages/flutter_breez_liquid/macos/Classes/breez_sdk_liquidFFI.h

# Update version number on podspec files & CMake scripts
version:
	bash scripts/version.sh